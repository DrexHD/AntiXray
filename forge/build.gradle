plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

loom {
    forge {
        mixinConfig "antixray.common.mixins.json", "antixray.forge.mixins.json"
        convertAccessWideners.set true
    }
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft}-${project.forge_version}"

    // Common
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }

    include(forgeRuntimeLibrary("com.moandjiezana.toml:toml4j:${project.toml_version}"))

    // Mixin Extras
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${project.mixin_extras}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${project.mixin_extras}"))

    // Allows running other mods in dev environment
    modLocalRuntime(fileTree(dir: 'runtime-mods', include: '*.jar'))
}

tasks.withType(org.gradle.jvm.tasks.Jar).configureEach {
    archiveAppendix.set("forge")
}

shadowJar {
    exclude "architectury.common.json"

    mergeServiceFiles()

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

processResources {
    inputs.property "mod_version", mod_version

    filesMatching("META-INF/mods.toml") {
        expand "mod_version": mod_version
    }
}